{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
?
-}

() recv_internal() {
}

int bitsize(int x) asm "BITSIZE";
int pow2(int x) asm "POW2";
(slice, int) startsFrom(slice s, slice flag) asm "SDBEGINSXQ";

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  slice s = linked_list.begin_parse();
  (int chunk, int refs) = slice_bits_refs(s);
  tuple builders = empty_tuple();
  int bn = 1;
  int remSpace = 1023;
  builder b = begin_cell();
  slice buff = s~load_bits(chunk);
  int buffL = chunk;
  chunk = 0;
  int fl = bitsize(flag) - 1;
  ;; ~dump(fl);
  int repl = bitsize(value) - 1;
  ;; ~dump(repl);
  slice fs = begin_cell().store_uint(flag, fl).end_cell().begin_parse();

  int exit = 0;
  do
  {
    while ((buffL >= fl)) 
    {
      if (buff~startsFrom(fs))
      {
        ;; ~dump(990);
        buffL -= fl;
        if (remSpace >= repl)
        {
          b~store_uint(value, repl);
          remSpace -= repl;
        }
        else
        {
          if (remSpace > 0)
          {
            int p1 = value >> (repl - remSpace);
            b~store_uint(p1, remSpace);
          }
          builders = cons(b, builders);
          bn += 1;
          b = begin_cell();
          int mask = pow2(repl - remSpace) - 1;
          int p2 = value & mask;
          b~store_uint(p2, repl - remSpace);
          remSpace = 1023 - (repl - remSpace);
        }
      }
      else
      {
        slice sbit = buff~load_bits(1);
        buffL -= 1;
        if (remSpace < 1)
        {
          builders = cons(b, builders);
          b = begin_cell();
          remSpace = 1023;
          bn += 1;
        }
        b = b.store_slice(sbit);
        remSpace -= 1;
      }
    }

    if (chunk == 0)
    {
      if (refs > 0)
      {
        s = s~load_ref().begin_parse();
        (chunk, refs) = slice_bits_refs(s);
        (int chunk, int refs) = slice_bits_refs(s);
      }
    }
    
    builder newBuff = begin_cell();
    newBuff = newBuff.store_slice(buff);
    if (chunk > 0)
    {
      int npsize = min(chunk, 1023 - buffL);
      newBuff = newBuff.store_slice(s~load_bits(npsize));
      buff = newBuff.end_cell().begin_parse();
      buffL += npsize;
      chunk -= npsize;
      ;; ~dump(111);
      ;; ~dump(npsize);
      ;; ~dump(buffL);
    }
    else
    {
      if (refs == 0)
      {
        ;; ~dump(999);

        exit = -1;
        if (remSpace >= buffL)
        {
          b = b.store_slice(buff);
        }
        else
        {
          b = b.store_slice(buff~load_bits(remSpace));
          builders = cons(b, builders);
          b = begin_cell();
          bn += 1;
          b = b.store_slice(buff);
        }
      }
      ;; write remaining buff
    }
  } until (exit != 0);


  cell outer = b.end_cell();
  repeat (bn - 1)
  {
    (builder tmp, builders) = uncons(builders);
    outer = tmp.store_ref(outer).end_cell();
  }

  return outer;
}



