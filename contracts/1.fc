{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}
int tlen(tuple t) asm "TLEN";

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (cell_hash(tree) == hash) { return tree; }
  tuple l = empty_tuple(); l = cons(tree, l); int n = 1;
  while (n > 0) {
    (tree, l) = uncons(l);
    n -= 1;
    slice s = tree.begin_parse();
    repeat(slice_refs(s))
    {
      cell c = s~load_ref();
      if (cell_hash(c) == hash)
      {
        return c;
      }
      if (cell_depth(c) > 0)
      {
        l = cons(c, l);
        n += 1;
      }
    }
  }
  return begin_cell().end_cell();
}
